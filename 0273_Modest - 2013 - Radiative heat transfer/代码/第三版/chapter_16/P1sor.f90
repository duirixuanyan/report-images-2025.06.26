!*******************************************************************************************************************
    SUBROUTINE P1SOR(II,JJ,KK,IRE,L,R,EPSX,EPSR,SX,SR,KT,ST,A1,SS,G,QX,QR)
! SUBROUTINE P1SUB CALCULATES INTERNAL INCIDENT RADIATION G AND WALL FLUXES Q
! FOR A 2-DIMENSIONAL RECTANGULAR OR AXISYMMETRIC ENCLOSURE BY THE P-1 APPROXIMATION
! ALLOWS FOR ARBITRARY SPATIAL VARIATION OF ABSORPTION/SCATTERING COEFFICIENTS, AS
! WELL AS LINEAR-ANISOTROPY, AND ALSO WALL AND INTERNAL SOURCES
! CAN BE USED FOR A GRAY MEDIUM OR ON A SPECTRAL BASIS
! USES SUCCESSIVE OVERRELAXTION (SOR) METHOD WITH OPTIMIZED SOR PARAMETER TO SOLVE SIMULTANEOUS EQUATIONS
! INPUT:
!   II  =   NUMBER OF NODES IN X-DIRECTION
!   JJ  =   NUMBER OF NODES IN Y- OR R-DIRECTION
!   KK  =   0 FOR RECTANGULAR, KK  =  1 FOR CYLINDRICAL ENCLOSURE
!   IRE =   RADIATIVE EQUILIBRIUM IDENTIFIER; IRE=0: NO EQUILIBRIUM; IRE=1: RADIATIVE EQUILIBRIUM 
!   L   =   LENGTH OF ENCLOSURE (CM)
!   R   =   HEIGHT (RECTANGLE) OR RADIUS (CYLINDER) OF ENCLOSURE (CM)
!   EPSX=   WALL EMITTANCES, EPSX(1) AT X=0, EPSX(2) AT X=L
!   EPSR=   WALL EMITTANCES, EPSR(1) AT Y=0 (FOR RECTANGLE ONLY), EPSR(2) AT Y,r=R
!   SX  =   SOURCES AT X-DIR WALLS:
!           SX(1,j=1,2,...JJ) SOURCE AT X=0 FOR VARYING Y/r-NODES
!           SX(2,j=1,2,...JJ) SOURCE AT X=L FOR VARYING Y/r-NODES
!           (FOR A STANDARD, GRAY APPLICATION SX=4*SIGMA*T^4, IN W/CM^2)
!   SR  =   SOURCES AT Y/r-DIR WALLS:
!           SR(1,i=1,2,...II) SOURCE AT Y=0 FOR VARYING X-NODES (RECTANGLE ONLY)
!           SR(2,i=1,2,...II) SOURCE AT Y/r=R FOR VARYING X-NODES
!           (FOR A STANDARD, GRAY APPLICATION SR=4*SIGMA*T^4, IN W/CM^2)
!   KT  =   KT(II,JJ) = ABSORPTION COEFFICIENT FOR ALL INTERNAL NODES (CM^-1)
!   ST  =   ST(II,JJ) = SCATTERING COEFFICIENT FOR ALL INTERNAL NODES (CM^-1)
!   A1  =   A1(II,JJ) = LINEAR ANISOTROPY FACTOR FOR ALL INTERNAL NODES
!   SS  =   SS(II,JJ) = SOURCES FOR ALL INTERNAL NODES (W/CM^2)
!           (FOR A STANDARD, GRAY APPLICATION SS=4*SIGMA*T^4, IN W/CM^2)
! OUTPUT:
!   G   =   G(II,JJ) = INCIDENT RADIATION FOR ALL INTERNAL NODES (W/CM^2)
!   QX  =   FLUXES AT X-DIR WALLS:
!           QX(1,j=1,2,...JJ) FLUX AT X=0 FOR VARYING Y/r-NODES
!           QX(2,j=1,2,...JJ) FLUX AT X=L FOR VARYING Y/r-NODES
!           (POSITIVE INTO POSITIVE X-DIRECTION)
!   SR  =   FLUXES AT Y/r-DIR WALLS:
!           SR(1,i=1,2,...II) FLUX AT Y=0 FOR VARYING X-NODES (RECTANGLE ONLY)
!           SR(2,i=1,2,...II) FLUX AT Y/r=R FOR VARYING X-NODES
!           (POSITIVE INTO POSITIVE Y/r-DIRECTION)
!
      IMPLICIT NONE
      INTEGER             :: II,JJ,KK,IRE,I,J,IM1,IP1,JM1,JP1,NUMIT,ITMAX,ICONV
      DOUBLE PRECISION    :: QR(2,II),QX(2,JJ),EPSR(2),EPSX(2),BR(2),BX(2),GN
      DOUBLE PRECISION    :: AA(II,JJ),BB(II,JJ),CC(II,JJ),DD(II,JJ),EE(II,JJ),FF(II,JJ),G(II,JJ),L,R
      DOUBLE PRECISION    :: A1(II,JJ),KT(II,JJ),ST(II,JJ),BT(II,JJ),SS(II,JJ),SR(2,II),SX(2,JJ)
      DOUBLE PRECISION    :: XTRA,SIP,OM,SUMER,YUN,DNP,DELX,DELR,DR2,SRCT
      IF(IRE/=0 .AND. IRE/=1) THEN
            WRITE(*,*) 'ILLEGAL VALUE IRE =',IRE,'  EXECUTION TERMINATED'
            STOP
            ENDIF
      IF(KK/=0 .AND. KK/=1) THEN
            WRITE(*,*) 'ILLEGAL VALUE KK =',KK,'  EXECUTION TERMINATED'
            STOP
            ENDIF
      BR=1.5D0*EPSR/(2.D0-EPSR)     ! BOUNDARY CONDITION FUNCTION
      BX=1.5D0*EPSX/(2.D0-EPSX)     ! BOUNDARY CONDITION FUNCTION
      BT=KT+ST                      ! EXTINCTION COEFFICIENT
      BT=BT-A1*ST/3.D0              ! MODIFIED EXTINCTION COEFFICIENT
      IF(JJ>1) THEN
        DELR=R/(JJ-1.D0)
      ELSE
        DELR=1.D10                  ! FOR 1D SLAB (JJ=1)
      ENDIF
      IF(II>1) THEN
        DELX=L/(II-1.D0)
      ELSE
        DELX=1.D10                  ! FOR 1D CYLINDER (II=1)
      ENDIF
      DR2=(DELX/DELR)**2
!
! COEFFICIENTS FOR S.O.R.
!
! INTERNAL
      DO I=1,II
        IM1=I-1+int(1/I)            ! i-1 with a minimum of 1 at i=1 (to avoid out-of-bounds call)
        IP1=I+1-int(I/II)           ! i+1 with a maximum of II at i=II (to avoid out-of-bounds call)
        DO J=1,JJ
            JM1=J-1+int(1/J)        ! j-1 with a minimum of 1 at j=1 (to avoid out-of-bounds call)  
            JP1=J+1-int(J/JJ)       ! j+1 with a maximum of JJ at j=JJ (to avoid out-of-bounds call)
            AA(I,J)=2.D0*BT(I,J)/(BT(IM1,J)+BT(I,J)+1.D-16)      ! coefficient for G(i-1,j)
            BB(I,J)=2.D0*BT(I,J)/(BT(IP1,J)+BT(I,J)+1.D-16)      ! coefficient for G(i+1,j)
            IF(KK==0) THEN
                CC(I,J)=2.D0*DR2*BT(I,J)/(BT(I,JM1)+BT(I,J)+1.D-16)  ! coefficient for G(i,j-1) for rectangle
                DD(I,J)=2.D0*DR2*BT(I,J)/(BT(I,JP1)+BT(I,J)+1.D-16)  ! coefficient for G(i,j+1) for rectangle
            ELSEIF(J>1) THEN
                CC(I,J)=2.D0*DR2*BT(I,J)/(BT(I,JM1)+BT(I,J)+1.D-16)*(1.D0-.5D0/JM1)  ! coefficient for G(i,j-1) for cylinder
                DD(I,J)=2.D0*DR2*BT(I,J)/(BT(I,JP1)+BT(I,J)+1.D-16)*(1.D0+.5D0/JM1)  ! coefficient for G(i,j+1) for cylinder
            ELSE
                CC(I,J)=0.D0                ! cylinder centerline
                DD(I,J)=2.D0*(KK+1)*DR2     ! cylinder centerline
            ENDIF
            SRCT=3.D0*BT(I,J)*DELX*DELX
            FF(I,J)=SRCT*(IRE+(1-IRE)*KT(I,J))*SS(I,J)                       ! nonhomogeneous/source term (i,j)
            EE(I,J)=SRCT*(1-IRE)*KT(I,J)+AA(I,J)+BB(I,J)+CC(I,J)+DD(I,J)     ! coefficient for G(i,j)
        ENDDO
      ENDDO
! R-WALLS                                    correction to G-coefficients using artificial nodes
      DO I=1,II
        CC(I,JJ)=CC(I,JJ)+DD(I,JJ)
        XTRA=2.D0*BT(I,JJ)*DELR*BR(2)*DD(I,JJ)
        EE(I,JJ)=EE(I,JJ)+XTRA
        FF(I,JJ)=FF(I,JJ)+XTRA*SR(2,I)
        DD(I,JJ)=0.D0
        IF(KK==0) THEN
            DD(I,1)=CC(I,1)+DD(I,1)
            XTRA=2.D0*BT(I,1)*DELR*BR(1)*CC(I,1)
            EE(I,1)=EE(I,1)+XTRA
            FF(I,1)=FF(I,1)+XTRA*SR(1,I)
            CC(I,1)=0.D0
        ENDIF
      ENDDO
! X-WALLS                                   correction to G-coefficients using artificial nodes
      DO J=1,JJ
        BB(1,J)=AA(1,J)+BB(1,J)
        XTRA=2.D0*BT(1,J)*DELX*BX(1)*AA(1,J)
        EE(1,J)=EE(1,J)+XTRA
        FF(1,J)=FF(1,J)+XTRA*SX(1,J)
        AA(1,J)=0.D0
        AA(II,J)=AA(II,J)+BB(II,J)
        XTRA=2.D0*BT(II,J)*DELX*BX(2)*BB(II,J)
        EE(II,J)=EE(II,J)+XTRA
        FF(II,J)=FF(II,J)+XTRA*SX(2,J)
        BB(II,J)=0.D0
      ENDDO
!
! NORMALIZE COEFFICIENTS
      DO I=1,II
        DO J=1,JJ
            AA(I,J)=AA(I,J)/EE(I,J)
            BB(I,J)=BB(I,J)/EE(I,J)
            CC(I,J)=CC(I,J)/EE(I,J)
            DD(I,J)=DD(I,J)/EE(I,J)
            FF(I,J)=FF(I,J)/EE(I,J)
        ENDDO
      ENDDO
!
! S.O.R. ITERATION
!
      SIP=1.D-6      ! STOPPING CRITERION FOR ITERATION  (see SOR below)
      NUMIT=0
      ITMAX=800 
      G=0
      OM=1.4
! ROUTINE TO OPTIMIZE OVERRELAXATION CONSTANT OM
  70  CALL SOR(DNP,DNP,YUN,SIP,0,OM,ICONV,ITMAX,NUMIT,OM,6)
      SUMER=0.D0
      YUN=0.D0
      DO 100 J=1,JJ
          JM1=J-1+int(1/J)
          JP1=J+1-int(J/JJ)
          DO 100 I=1,II
             IM1=I-1+int(1/I)
             IP1=I+1-int(I/II)
             GN=OM*(CC(I,J)*G(I,JM1)+AA(I,J)*G(IM1,J) &
                         +DD(I,J)*G(I,JP1)+BB(I,J)*G(IP1,J) &
                         +FF(I,J))+(1.D0-OM)*G(I,J)
             SUMER=SUMER+(GN-G(I,J))**2
             G(I,J)=GN
             IF(GN.GT.YUN) YUN=GN
 100  CONTINUE
 
      DNP=SUMER/YUN
!
! CONVERGENCE CHECK
!
      IF(ICONV.EQ.0) GOTO 70
      IF(NUMIT.GT.ITMAX) WRITE(6,1300)
      WRITE(6,1000) NUMIT,DNP
!
! CALCULATION OF WALL FLUXES
!
      DO 210 I=1,II
      IF(KK==0) QR(1,I)=(SR(1,I)-G(I,1))/3.D0*BR(1)
 210  QR(2,I)=(G(I,JJ)-SR(2,I))/3.D0*BR(2)
      DO 220 J=1,JJ
      QX(1,J)=(SX(1,J)-G(1,J))/3.D0*BX(1)
 220  QX(2,J)=(G(II,J)-SX(2,J))/3.D0*BX(2)
1000  FORMAT(//5X,'NUMBER OF ITERATIONS:',I3,'  2-NORM ERROR:',E10.3/)
1300  FORMAT(//10X,'ITERATION DID NOT CONVERGE!'/)
      RETURN
      END
!*******************************************************************************************************************

!*******************************************************************************************************************
    SUBROUTINE SOR(DELNP,DELNE,YUN,SIP,NPRT,OM,ICONV,ILIMIT,ITP,OME,IO)
!
! SUBROUTINE SOR IS AN IMPLEMENTATION OF ALGORITHM 9-6.1 GIVEN IN
! *APPLIED ITERATIVE METHODS* BY LOUIS A. HAGEMAN AND DAVID M. YOUNG,
! ACADEMIC PRESS, 1981.
!
! SOR COMPUTES THE OVERRELAXATION PARAMETER OMEGA AND MEASURES THE
! ITERATION ERROR VECTOR FOR THE SUCCESSIVE OVERRELAXATION METHOD.
! THIS SUBROUTINE MUST BE ENTERED BEFORE EVERY SOR ITERATION. ON EXIT,
! OM IS THE VALUE OF OMEGA TO USE ON THE NEXT ITERATION.
!
!  ITP    = # OF ITERATION (=0 ON FIRST ENTRY)
!  OME    = OLD (OR INITIAL) VALUE FOR OMEGA
!  ILIMIT = UPPER LIMIT OF ITERATIONS
!  IO     = OUTPUT UNIT FOR PRINTING
! REMARK 1: SET OME=1.0 INITIALLY UNLESS SOME A-PRIORI INFORMATION IS
!       KNOWN.
! REMARK 2: IF FIXED VALUE OF OMEGA IS TO BE USED SET OME TO THE
!       NEGATIVE OF DESIRED VALUE.
! REMARK 3: AFTER FIRST ENTRY USER SHOULD NOT MODIFY ANY VARIABLES
!       EXCEPT DELNE,DELNP,YUN,SIP, AND NPRT.
!  NPRT   = PRINT CONTROL: =0 PRINTING ONLY AFTER CONVERGENCE
!              =1 PRINTING AFTER EVERY ITERATION.
!  DELNP  = THE 2-NORM OF THE DIFFERENCE VECTOR,EQ. 9-4.1.
!  DELNE  = THE BETA-NORM OF THE DIFFERENCE VECTOR.
!  YUN    = THE ETA-NORM OF THE LATEST SOLUTION VECTOR.
!  SIP    = STOPPING CRITERION NUMBER, ZETA, EQ. 9-5.17.
!  OM     = VALUE OF OMEGA FOR NEXT ITERATION
!  ICONV  = CONVERGENCE INDICATOR: =0 NO CONVERGENCE
!                  =1 CONVERGENCE
!                  =10 ILIMIT EXCEEDED
!  ITP    = ITERATION COUNTER.
!  DELNE  = ON EXIT GIVES ESTIMATE OF ITERATION ERROR (CF. SIP)
!
! WHENEVER OMEGA IS CHANGED AND NPRT=1, THE FOLLOWING IS PRINTED:
!  OMEG P. = NEW UNCORRECTED ESTIMATE OF OMEGA;
!  OMEG U. = NEW OMEGA TO BE USED AFTER TAU-TEST, EQ. 9-5.22;
!  P-HAT   = VALUE DEFINED BY EQ. 9-6.2
!  P-STAR  = VALUE DEFINED BY EQ. 9-5.20
!  MU      = ESTIMATE OF SPECTRAL RADIUS OF JACOBI MATRIX
!
!
    IMPLICIT DOUBLE PRECISION (A-H,O-Z)
    INTEGER P,PS,PH
    DOUBLE PRECISION TAU(8)
    DATA TAU/1.6,1.8,1.9,1.95,1.975,1.985,1.990,1.995/
    ICONV=0
    EPS=1.
    XC=1.
    IPWM=0
    IF(ITP.GT.0) GOTO 130
!
! INITIALIZE
    P=-1
    IS=0
    Z=DABS(OME)
    IF(Z.LT.1..OR.Z.GT.2.) OME=1.
    XMUP=0.
    IF(Z.GT.1.) XMUP=2.*DSQRT(Z-1.)/Z
    OMEP=OME
    DELNP=1.
    INPRT=0
    R=1.
    FF=.75
    PSPP=.5
    RSPP=.0001
!
! NEXT ITERATION
10   ITP=ITP+1
    P=P+1
    DELNO=DELNP
    IF(P.GT.0) GOTO 80
! INITIALIZE FOR NEW ESTIMATE OF OMEGA
    ICS=0
    IS=IS+1
    PH=3
    PS=6
    OME=OMEP
    IF(IS.GT.8) IS=8
    TAUS=TAU(IS)
    IF(OME.GT.TAUS) OME=TAUS
    IF(OMEP.LT.0.) OME=-OMEP
    XOM1=OME-1.
    XC1=XOM1/(2.-OME)
    IPH=INT(XC1)
    IF(IPH.GT.PH) PH=IPH
    XOCR=1.
    IF(XOM1.GT.0.) XOCR=DLOG(XOM1)
    DO 20 I=PS,5000
    Z=FLOAT(I)
    J=I-1
    IF(Z*XOM1**J.LT.PSPP) GOTO 30
20   CONTINUE
    I=5000
30   PS=I
    IF(OMEP.GT.0.)  GOTO 40
    PS=10000
    GOTO 50
40   IF(XOM1.GT.0.) GOTO 50
    PH=2
    PS=3
    IS=IS-1
50   XWDEL=RSPP
    IF(OME.LE.1.9) GOTO 60
    Z=((2.-OME)**2)*.01
    IF(Z.LT.RSPP) XWDEL=Z
60   IPWM=1
70   FORMAT(1H0,8HOMEG P.=,E11.4,11H   OMEG U.=,E11.4,9H   P-HAT=, &
        I3,10H   P-STAR=,I5,6H   MU=,E11.4)
!
! CLEAN UP BEFORE EXIT
80   DELNE=EPS
    ITPP=ITP-1
    IF(ITPP.GE.ILIMIT) ICONV=10
    IF(ICONV.GT.0) GOTO 190
    IF(ITP.EQ.1) WRITE(IO,85)
85   FORMAT(1H0,31HINITIAL ESTIMATE USED FOR OMEGA)
    IF(ITP.EQ.1) GOTO 110
    IF(NPRT.EQ.0) GOTO 120
90   WRITE(IO,100) ITPP,R,XC,EPS,ICONV
100   FORMAT(1H ,2HI=,I4,5H   R=,E11.4,6H   CR=,E11.4,7H   EPS=,E11.4, &
               8H  ICONV=,I3)
110   IF(IPWM.EQ.1) WRITE(IO,70) OMEP,OME,PH,PS,XMUP
    IF(PS.GE.10000.AND.ICONV.GT.0) GOTO 200
120   OM=OME
    RETURN
!
! STOPPING TEST
130   RO=R
    R=DELNP/DELNO
    H=R
    IF(ITP.EQ.2) RESDS=DELNP
    IF(P.LT.PH) GOTO 10
    IF(R.GE.1.) GOTO 10
    IF(R.GT.XOM1) GOTO 140
    ICIS=ITP
    H=XOM1
    ICS=ICS+1
140   EPS=DELNE/(YUN*(1.-H))
    IF(XOM1.GT.0.) XC=DLOG(R)/XOCR
    IF(EPS.LT.SIP) ICONV=1
!
! PARAMETER CHANGE TEST
150   IF(P.LT.PS) GOTO 10
    IF(ICS.EQ.0) GOTO 160
    ITPD=ITP-ICIS
! THIS TEST INVOLVING ICS AND ITPD IS NOT INCLUDED IN ALG. 9-6.1.
    IF(ICS.LT.3.AND.ITPD.GT.40) GOTO 160
    GOTO 10
160   IF(OME.GT.1.) GOTO 170
    Z=1.-R
    OMEP=2./(1.+DSQRT(Z))
    P=-1
    XMUP=DSQRT(R)
    GOTO 10
170   Z=XOM1**FF
    IF(R.LT.Z) GOTO 10
    DELR=RO-R
    Z=-10.*XWDEL
    IF(DELR.LT.XWDEL.AND.DELR.GE.Z) GOTO 180
    IF(IS.GT.2) GOTO 10
    Z=XOM1**.1
    IF(R.LT.Z) GOTO 10
180   XMUP=(R+XOM1)/(OME*DSQRT(R))
    Z=1.-XMUP*XMUP
    OMEP=2./(1.+DSQRT(Z))
    P=-1
    GOTO 10
!
! PRINT FINAL ESTIMATE FOR OMEGA IF NPRT=0
190   IF(INPRT.NE.0) GOTO 90
    IF(PS.GE.10000) GOTO 90
    IF(NPRT.EQ.1) GOTO 90
    WRITE(IO,195)
195   FORMAT(1H0,17HITERATION SUMMARY)
    INPRT=1
    WRITE(IO,70) OMEP,OME,PH,PS,XMUP
    GOTO 90
!
! IF OMEGA IS FIXED, GIVE ESTIMATE OF ITS WORTH.
200   IF(INPRT.NE.0) GOTO 120
    INPRT=1
    DELNP=-1.
    IF(R.LT.1.) GOTO 250
210   WRITE(IO,220) OME
220   FORMAT(1H0,35HNUMERICAL RESULTS IMPLY THAT OMEGA=,E11.4,&
        47H IS GREATER THAN OR EQUAL TO THE OPTIMAL VALUE.)
230   WRITE(IO,240)
240   FORMAT(1H0)
    GOTO 120
250   IPRED=1
    IF(ICS.EQ.0) GOTO 260
    ITPD=ITP-ICIS-1
    IF(ICS.LT.3.AND.ITPD.GT.40) GOTO 260
    GOTO 210
260   Z=.0001
    ZZ=-.001
    DELR=RO-R
    IF(DELR.LE.Z.AND.DELR.GE.ZZ) IPRED=2
    IF(OME.EQ.1.) GOTO 290
    XMUP=(R+XOM1)/(OME*DSQRT(R))
270   Z=1.-XMUP*XMUP
    DELNP=2./(1.+DSQRT(Z))
    WRITE(IO,280) DELNP
280   FORMAT(1H0,'NUMERICAL RESULTS INDICATE THAT THE OPTIMAL OMEGA' &
        ,' IS ABOUT ',E11.4)
    IF(IPRED.EQ.1) WRITE(IO,300)
    IF(IPRED.EQ.2) WRITE(IO,310)
    Z=1.
    IF(IPRED.EQ.1) Z=-1.
    DELNP=Z*DELNP
    GOTO 230
290   XMUP=DSQRT(R)
    GOTO 270
300   FORMAT(1H ,30HTHIS ESTIMATE IS QUESTIONABLE.)
310   FORMAT(1H ,28HTHIS ESTIMATE IS REASONABLE.)
    END
