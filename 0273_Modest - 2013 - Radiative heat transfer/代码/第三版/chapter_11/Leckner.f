      PROGRAM LECKNER
C STAND-ALONE FRONT END TO CALL SUBROUTINES TOTEMISS AND TOTABSOR TO CALCULATE
C TOTAL EMISSIVITY AND ABSORPTIVITY OF A CARBON DIOXIDE-WATER VAPOR MIXTURE, 
C USING LECKNER'S CORRELATION
C INPUT:
C   PH2O    = PARTIAL PRESSURE OF STEAM (BAR)
C   PCO2    = PARTIAL PRESSURE OF CARBON DIOXIDE (BAR)
C   PTOT    = TOTAL PRESSURE OF GAS MIXTURE (BAR)
C   TG  = GAS TEMPERATURE (K)
C   TW  = WALL TEMPERATURE (K)
C   XL  = GEOMETRIC PATH LENGTH (CM)
C OUTPUT:
C   EPSH2O= TOTAL EMISSIVITY OF WATER VAPOR
C   EPSCO2= TOTAL EMISSIVITY OF CARBON DIOXIDE
C   EPSTOT= TOTAL EMISSIVITY OF GAS MIXTURE (CONSIDERING OVERLAP EFFECTS)
C   ABSH2O= TOTAL EMISSIVITY OF WATER VAPOR
C   ABSCO2= TOTAL EMISSIVITY OF CARBON DIOXIDE
C   ABSTOT= TOTAL EMISSIVITY OF GAS MIXTURE (CONSIDERING OVERLAP EFFECTS)
C
      implicit real*8 (a-h,o-z)
      write(*,*) 'input ph2o(bar),pco2(bar),ptot(bar),Tg(K),Tw(K),l(cm)'
      read(*,*) ph2o,pco2,ptot,Tg,Tw,xl
      call TOTEMISS(PH2O,PCO2,PTOT,TG,XL,EPSH2O,EPSCO2,EPSTOT)
      call TOTABSOR(PH2O,PCO2,PTOT,TG,TW,XL,ABSH2O,ABSCO2,ABSTOT)
      write(*,100) EPSH2O,ABSH2O,EPSCO2,ABSCO2,EPSTOT,ABSTOT
 100    format('H20: Column emissivity:',f6.4,',  absorptivity:',f6.4/
     &       'CO2: Column emissivity:',f6.4,',  absorptivity:',f6.4/
     &       'Mix: Column emissivity:',f6.4,',  absorptivity:',f6.4)
      stop
      end

      SUBROUTINE TOTEMISS(PH2O,PCO2,PTOT,TG,XL,EPSH2O,EPSCO2,EPSTOT)
C CALCULATES THE TOTAL EMISSIVITY OF A STEAM-CARBON DIOXIDE-NITROGEN MIXTURE,
C USING THE CORRELATION OF LECKNER
C INPUT:
C   PH2O    = PARTIAL PRESSURE OF STEAM (BAR)
C   PCO2    = PARTIAL PRESSURE OF CARBON DIOXIDE (BAR)
C   PTOT    = TOTAL PRESSURE OF GAS MIXTURE (BAR)
C   TG  = GAS TEMPERATURE (K)
C   XL  = GEOMETRIC PATH LENGTH (CM)
C OUTPUT:
C   EPSH2O= TOTAL EMISSIVITY OF WATER VAPOR
C   EPSCO2= TOTAL EMISSIVITY OF CARBON DIOXIDE
C   EPSTOT= TOTAL EMISSIVITY OF GAS MIXTURE (CONSIDERING OVERLAP EFFECTS)
C
C***** Make sure all quantities are declared double precision!!! *****
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION CMNH2O(3,3),CMNCO2(4,3)
      DATA CMNH2O /-2.2118D0,-1.1987D0,0.035596D0, 0.85667D0,
     >  0.93048D0,-0.14391D0,-0.10838D0,-0.17156D0,0.045915D0/
      DATA CMNCO2 /-3.9893D0,2.7669D0,-2.1081D0,0.39163D0,
     >  1.2710D0,-1.1090D0,1.0195D0,-0.21897D0,-0.23678D0,
     >  0.19731D0,-0.19544D0,0.044644D0/
      T=TG/1.D3
C
C--H2O
C
      IF(T.LE..75D0) THEN
        A=2.144D0
        ELSE
        A=1.888D0-2.053D0*DLOG10(T)
        ENDIF
      B=1.10D0/T**1.4
      C=.5D0
      PALM=1.32D1*T*T
      PE=PTOT+2.56D0*PH2O/DSQRT(T)
      PAL=PH2O*XL
      EPSH2O=0.D0
      IF(PAL.LT.1.D-3) GOTO 15
      PALL=DLOG10(PAL)
      EPS0=0.D0
      DO 10 I=0,2
      DO 10 J=0,2
  10    EPS0=EPS0+CMNH2O(J+1,I+1)*T**J*PALL**I
      EPS0=DEXP(EPS0)
      EPSR=1.D0-(A-1.D0)*(1.D0-PE)/(A+B-1.D0+PE)
     >      *DEXP(-C*(DLOG10(PALM)-PALL)**2)
      EPSH2O=EPS0*EPSR
C
C--CO2
C
  15  A=1.D0+0.1D0/T**1.45
      B=0.23D0
      C=1.47D0
      IF(T.LE..7D0) THEN
        PALM=0.054D0/(T*T)
        ELSE
        PALM=0.225D0*T*T
        ENDIF
      PE=PTOT+0.28D0*PCO2
      PAL=PCO2*XL
      EPSCO2=0.D0
      IF(PAL.LT.1.D-3) GOTO 25
      PAL=DLOG10(PAL)
      EPS0=0.D0
      DO 20 I=0,2
      DO 20 J=0,3
  20    EPS0=EPS0+CMNCO2(J+1,I+1)*T**J*PAL**I
      EPS0=DEXP(EPS0)
      EPSR=1.D0-(A-1.D0)*(1.D0-PE)/(A+B-1.D0+PE)
     >      *DEXP(-C*(DLOG10(PALM)-PAL)**2)
      EPSCO2=EPS0*EPSR
C
C--OVERLAP
C
  25  ZETA=PH2O/(PH2O+PCO2)
      PAL=(PH2O+PCO2)*XL
      IF(PAL.LE.1.D0) THEN
        DEPS=0.D0
      ELSE
        DEPS=(ZETA/(1.07D1+1.01D2*ZETA)-ZETA**10.4/1.117D2)
     >    *(DLOG10(PAL))**2.76
      ENDIF
      EPSTOT=EPSH2O+EPSCO2-DEPS
      RETURN
      END
C************************************************************************
      SUBROUTINE TOTABSOR(PH2O,PCO2,PTOT,TG,TW,XL,ABSH2O,ABSCO2,ABSTOT)
C CALCULATES THE TOTAL ABSORPTIVITY OF A STEAM-CARBON DIOXIDE-NITROGEN MIXTURE,
C USING THE CORRELATION OF LECKNER
C INPUT:
C   PH2O    = PARTIAL PRESSURE OF STEAM (BAR)
C   PCO2    = PARTIAL PRESSURE OF CARBON DIOXIDE (BAR)
C   PTOT    = TOTAL PRESSURE OF GAS MIXTURE (BAR)
C   TG  = GAS TEMPERATURE (K)
C   TW  = WALL TEMPERATURE (K)
C   XL  = GEOMETRIC PATH LENGTH (CM)
C OUTPUT:
C   ABSH2O= TOTAL EMISSIVITY OF WATER VAPOR
C   ABSCO2= TOTAL EMISSIVITY OF CARBON DIOXIDE
C   ABSTOT= TOTAL EMISSIVITY OF GAS MIXTURE (CONSIDERING OVERLAP EFFECTS)
C
      IMPLICIT REAL*8 (A-H,O-Z)
      XLAD=XL*TW/TG
      CALL    TOTEMISS(PH2O,PCO2,PTOT,TW,XLAD,ABSH2O,ABSCO2,ABSTOT)
      TFAC=DSQRT(TG/TW)
      ABSH2O=ABSH2O*TFAC
      ABSCO2=ABSCO2*TFAC
      ABSTOT=ABSTOT*TFAC
      RETURN
      END
