      program voigtcl
   5   write(*,*) 'S,BL,BD,DETA?'
      read(*,*) S,BL,BD,DETA
      call VOIGT(S,BL,BD,DETA,absc)
      write(*,*) absc
      goto 5
      stop
      end
      SUBROUTINE VOIGT(S,BL,BD,DETA,KETA)
C COMPUTES ABSORPTION COEFFICIENT FOR A VOIGT LINE USING THE SUBROUTINE
C BY J. HUMLICEK, J. QUANT. SPECTR. RAD. TRANSFER, V.27, PP.437-444, 1982
C
C INPUT:
C   S     LINE INTENSITY (CM-2)
C   BL    LORENTZ HALFWIDTH AT HALFHEIGHT (CM-1)
C   BD    DOPPLER HALFWIDTH AT HALFHEIGHT (CM-1)
C   DETA  WAVENUMBERS AWAY FROM LINE CENTER (CM-1)
C OUTPUT:
C   KETA  SPECTRAL ABSORPTION COEFFICIENT AT A WAVENUMBER DETA REMOVED 
c         FROM LINE CENTER (CM-1)
C
      REAL KETA
      COMPLEX W4,Z
      RTLN2=0.8325546               ! SQRT(ALOG(2))
      RTPI=1.772454                 ! SQRT(PI)
      IF(BD.LT.1.E-10) BD=1.E-10    ! Avoid division by zero
C BD2 IS THE DOPPLER HALFWIDTH AT 1/e HEIGHT (CM-1)
      BD2=BD/RTLN2                  ! CORRECTED BD
C   
      Z=CMPLX(DETA/BD2,BL/BD2)
      KETA=S/(RTPI*BD2)*REAL(W4(Z))
      RETURN
      END
      FUNCTION W4(Z)
C COMPUTES THE COMPLEX PROBABILITY FUNCTION W(Z)=EXP(-Z**2)*ERFC(-I*Z)
C IN THE UPPER HALF-PLANE Z=X+I*Y (I.E. FOR Y>=0)
C MAXIMUM RELATIVE ERROR OF BOTH REAL AND IMAGINARY PARTS IS <1*10**(-4)
      COMPLEX W4,Z,T,U
      X=REAL(Z)
      Y=AIMAG(Z)
      T=CMPLX(Y,-X)
      S=ABS(X)+Y
      IF(S.LT.15.) GOTO 1
C  ***  REGION I
      W4=T*.5641896/(.5+T*T)
      RETURN
1     IF (S.LT.5.5) GOTO 2
C  ***  REGION II
      U=T*T
      W4=T*(1.410474+U*.5641896)/(.75+U*(3.+U))
      RETURN
2     IF (Y.LT..195*ABS(X)-.176) GOTO 3
C  ***  REGION III
      W4=(16.4955+T*(20.20933+T*(11.96482+T*(3.778987+T*.5642236))))/
     &   (16.4955+T*(38.82363+T*(39.27121+T*(21.69274+T*(6.699398+T)))))
      RETURN
C  ***  REGION IV
3     U=T*T
      W4=CEXP(U)-T*(36183.31-U*(3321.9905-U*(1540.787-U*(219.0313
     &  -U*(35.76683-U*(1.320522-U*.56419))))))
     &  /(32066.6-U*(24322.84-U*(9022.228-U*(2186.181-U*(364.2191
     &  -U*(61.57037-U*(1.841439-U)))))))
      RETURN
      END
